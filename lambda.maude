mod LAMBDA is
    including BOOL .
    including QID .

    sorts Variable Term .
    subsort Variable < Term .
    subsort Qid < Variable .

    --- Calculus
    op λ_._ : Variable Term -> Term [gather (e E) prec 121] .
    op __ : Term Term -> Term [gather (E e) prec 120] .
    op if_then_else_ : Bool Term Term -> Term .

    var b : Bool .
    var x : Variable .
    vars t₁ t₂ : Term .

    rl (λ x . t₁) t₂ => [x ↦ t₂]t₁ .

    crl if b then t₁ else t₂ => t₁ if b .
    crl if b then t₁ else t₂ => t₂ if not b .

    --- Free variables
    sort VariableSet .
    subsort Variable < VariableSet .

    op empty : -> VariableSet .
    op _,_ : VariableSet VariableSet -> VariableSet [assoc comm id: empty prec 100] .
    op _\_ : VariableSet VariableSet -> VariableSet [prec 101] .
    op _∉_ : Variable VariableSet -> Bool [prec 101] .

    var S₁ S₂ : VariableSet .

    eq x, x, S₁ = x, S₁ .

    eq S₁, x \ x, S₂  = S₁ \ S₂ .
    eq S₁ \ S₂        = S₁ [owise] .

    eq x ∉ x, S₁ = false .
    eq x ∉ S₁    = true [owise] .

    op FV_ : Term -> VariableSet .

    eq FV(x)        = x .
    eq FV(λ x . t₁) = FV(t₁) \ x .
    eq FV(t₁ t₂)    = FV(t₁), FV(t₂) .

    --- Substitution
    op [_↦_]_ : Variable Term Term -> Term .

    var y : Variable .
    var s : Term .

    eq  [x ↦ s]x          = s .
    ceq [x ↦ s]y          = y                  if x =/= y .
    eq  [x ↦ s](λ x . t₁) = (λ x . t₁) .
    ceq [x ↦ s](λ y . t₁) = (λ y . [x ↦ s]t₁)  if x =/= y /\ y ∉ FV(s) .
    eq  [x ↦ s](t₁ t₂)    = [x ↦ s]t₁ [x ↦ s]t₂ .

endm

mod CHURCH-BOOLEAN is
    including LAMBDA .

    ops tru fls test and or not : -> Term .

    vars t f : Variable .

    eq tru = λ 't . λ 'f . 't .
    eq fls = λ 't . λ 'f . 'f .

    eq test = λ 'l . λ 'm . λ 'n . 'l 'm 'n .

    eq and = λ 'b . λ 'c . 'b 'c fls .
endm

--- Examples
rew test tru 'v 'w .
rew test fls 'v 'w .

rew and tru tru .
rew and tru fls .
